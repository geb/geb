/*
 * Copyright 2023 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package geb.gradle.cloud.task

import org.gradle.api.DefaultTask
import org.gradle.api.tasks.Input
import org.gradle.api.tasks.OutputFile
import org.gradle.api.tasks.TaskAction

abstract class DownloadExternalTunnel extends DefaultTask {

    private String eTag

    @OutputFile
    File tunnelZip = project.file("${project.buildDir}/${outputPath()}")

    @Input
    String getETag() {
        eTag ?= fetchETag()
    }

    @TaskAction
    void download() {
        def url = downloadUrl()
        logger.info("Downloading {} to {}", url, tunnelZip)
        tunnelZip.withOutputStream { it << new URL(url).bytes }
    }

    abstract protected String outputPath()

    abstract protected String downloadUrl()

    private String fetchETag() {
        def connection = new URL(downloadUrl()).openConnection() as HttpURLConnection
        try {
            connection.requestMethod = "HEAD"
            connection.getHeaderField("etag")
        } finally {
            connection.disconnect()
        }
    }
}

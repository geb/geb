/*
 * Copyright 2015 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package downloading

import configuration.InlineConfigurationLoader
import geb.test.GebSpecWithCallbackServer
import org.junit.Rule
import org.junit.rules.TemporaryFolder

import javax.servlet.http.HttpServletRequest
import javax.servlet.http.HttpServletResponse

class DownloadingConfigurationSpec extends GebSpecWithCallbackServer implements InlineConfigurationLoader {

    @Rule
    TemporaryFolder temporaryFolder

    String getDirectDownloadingConfig() {
        """
            // tag::config[]
            defaultDownloadConfig = { HttpURLConnection connection ->
                connection.setRequestProperty("User-Agent", "Geb")
            }
            // end::config[]
        """
    }

    def "configuring direct downloading globally"() {
        given:
        callbackServer.get = { HttpServletRequest request, HttpServletResponse response ->
            response.contentType = "text/plain"
            response.outputStream << request.getHeader("User-Agent")
        }
        configScript(directDownloadingConfig)
        browser.config.rawConfig.merge(config.rawConfig)

        when:
        go()

        then:
        downloadText() == "Geb"
    }
}

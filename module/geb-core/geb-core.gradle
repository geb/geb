import geb.gradle.browserstack.BrowserStackAccount
import geb.gradle.saucelabs.SauceAccount
import geb.gradle.lambdatest.LambdaTestAccount

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'io.ratpack:ratpack-gradle:1.6.0'
    }
}

apply plugin: "geb-browserstack"
apply plugin: "geb-saucelabs"
apply plugin: "geb-lambdatest"
apply plugin: 'com.github.erdi.webdriver-binaries'
apply plugin: ratpack.gradle.RatpackBasePlugin

dependencies {
    compile seleniumDependency, seleniumSupportDependency

    compile project(":module:geb-ast")
    compile project(":module:geb-waiting")
    compile('org.jodd:jodd-lagarto:5.1.5') {
        exclude(group: "ch.qos.logback")
    }
    compile "org.threeten:threeten-extra:$threeTenExtraVersion"

    testCompile "cglib:cglib:2.2", jsoupDependency, ratpack.groovyTest

    sauceConnect sauceConnectDependency
}

webdriverBinaries {
    chromedriver {
        version = "86.0.4240.22"
        fallbackTo32Bit = true
    }
}

test {
    maxParallelForks availableProcessors
}

task allCrossBrowserTests {
    dependsOn 'allSauceLabsTests', 'allBrowserStackTests', 'allDockerisedCrossBrowserTests', 'allLambdaTestTests'
}

task firefoxLinuxTest(type: Test) {
    maxHeapSize = "512m"
    systemProperty "geb.dockerized.driver", "firefox"
    maxParallelForks 2
}

chromeLinuxTest {
    maxParallelForks 2
}

allDockerisedCrossBrowserTests.dependsOn firefoxLinuxTest

task localChromeTest(type: Test) {
    maxHeapSize = "512m"
    systemProperty "geb.local.driver", "chrome"
}

sauceLabs {
    browsers {
        safari_mac_8 {
            capabilities platform: "OS X 10.10"
        }
    }
    task {
        maxHeapSize = "512m"
        maxParallelForks 5
        testLogging {
            events "started", "skipped", "passed", "failed"
        }
    }
    account {
        username = System.getenv(SauceAccount.USER_ENV_VAR)
        accessKey = System.getenv(SauceAccount.ACCESS_KEY_ENV_VAR)
    }
    connect {
        timeout = 10 // minutes
        identifier = UUID.randomUUID().toString()
    }
}

browserStack {
    def applicationAddresses = [8000, 8080, 9000, 9090, 9999].collect { "http://localhost:$it" }
    application(*applicationAddresses)

    browsers {
        android {
            capabilities device: "Samsung Galaxy S6", realMobile: "true"
        }
        chrome_mac_78 {
            capabilities os: "OS X", os_version: "High Sierra"
        }
        chrome_windows_78 {
            capabilities os: "Windows", os_version: "10"
        }
        firefox_mac_47 {
            capabilities os: "OS X", os_version: "High Sierra"
        }
        create("internet explorer_windows_10") {
            capabilities os: "Windows", os_version: "8"
        }
        create("internet explorer_windows_11") {
            capabilities os: "Windows", os_version: "10"
        }
        create("edge_windows_18") {
            capabilities os: "Windows", os_version: "10"
        }
    }
    task {
        maxHeapSize = "512m"
        maxParallelForks 5
        testLogging {
            events "started", "skipped", "passed", "failed"
        }
    }
    account {
        username = System.getenv(BrowserStackAccount.USER_ENV_VAR)
        accessKey = System.getenv(BrowserStackAccount.ACCESS_KEY_ENV_VAR)
    }
    local {
        identifier = UUID.randomUUID().toString()
    }
}

lambdaTest {
    browsers {
        firefox_windows_70 {
            capabilities platform: "Windows 10"
        }
    }
    task {
        maxHeapSize = "512m"
        maxParallelForks 5
        testLogging {
            events "started", "skipped", "passed", "failed"
        }
    }
    account {
        username = System.getenv(LambdaTestAccount.USER_ENV_VAR)
        accessKey = System.getenv(LambdaTestAccount.ACCESS_KEY_ENV_VAR)
    }
    tunnelOps {
        tunnelName = UUID.randomUUID().toString()
    }
}


task nonIeBrowserStackTests {
    dependsOn 'androidTest', 'chromeMac78Test', 'chromeWindows78Test', 'firefoxMac47Test'
}

task ieBrowserStackTests {
    dependsOn 'internetExplorerWindows10Test', 'internetExplorerWindows11Test', 'edgeWindows18Test'
}

modifyPom { pom ->
    pom.project {
        name "Geb Core"
        description "Geb (pronounced \"jeb\") is a browser automation solution. It brings together the power of WebDriver, the elegance of jQuery content selection, the robustness of Page Object modelling and the expressiveness of the Groovy language."
    }

    pom.dependencies.findAll { it.groupId == "org.seleniumhq.selenium" }*.scope = "provided"
}
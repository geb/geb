dependencies {
	compile seleniumDependency, seleniumSupportDependency
	
	// This is not strictly a compile dependency.
	// We list it as one so that it becomes a transitive dependency of geb-core and therefore
	// making it available at compile time of client code, assuming that their dependency mangement
	// system makes transitive compile dependencies first class compile dependencies.
	compile project(":module:geb-implicit-assertions")
	compile project(":module:geb-ast")
}

test {
	maxParallelForks 2
	if (driver == "htmlunit") {
		exclude "**/ExoticAttributeValuesSpec.class"
	}
	crossBrowser.enable it
}

tasks.withType(Test).matching { it.hasProperty("crossBrowser") }.all {
	maxParallelForks 3
}

modifyPom { pom ->
	pom.project {
		name "Geb Core"
		description "Geb (pronounced \"jeb\") is a browser automation solution. It brings together the power of WebDriver, the elegance of jQuery content selection, the robustness of Page Object modelling and the expressiveness of the Groovy language."
	}
	
	pom.dependencies.findAll { it.groupId == "org.seleniumhq.selenium" }*.scope = "provided"
}